# vi: ft=fish

# -- HELPERS -----------------------------------------------------------------------------------------------------------

function cache_and_source
    set -l cache_file $argv[1]
    set -l cache_cmd $argv[2]
    set -l cache_path $__fish_cache_dir/$cache_file

    if not test -f $cache_path
        eval $cache_cmd >$cache_path
    end

    source $cache_path
end

function add_completion
    set -l completion_file $argv[1]
    set -l completion_cmd $argv[2]
    set -l completion_path $__fish_config_dir/completions/$completion_file

    if not test -e $completion_path
        eval $completion_cmd >$completion_path
    end
end

# -- XDG ---------------------------------------------------------------------------------------------------------------

# https://specifications.freedesktop.org/basedir-spec/basedir-spec-latest.html
set -q XDG_CONFIG_HOME; or set -Ux XDG_CONFIG_HOME $HOME/.config
set -q XDG_DATA_HOME; or set -Ux XDG_DATA_HOME $HOME/.local/share
set -q XDG_STATE_HOME; or set -Ux XDG_STATE_HOME $HOME/.local/state
set -q XDG_CACHE_HOME; or set -Ux XDG_CACHE_HOME $HOME/.cache
set -Ux XDG_RUNTIME_DIR $HOME/Library/Caches/TemporaryItems

for xdgdir in (path filter -vd $XDG_CONFIG_HOME $XDG_DATA_HOME $XDG_STATE_HOME $XDG_CACHE_HOME)
    mkdir -p $xdgdir
end

# -- COLORSCHEME -------------------------------------------------------------------------------------------------------

fish_config theme choose "Catppuccin Macchiato"

# -- CACHE -------------------------------------------------------------------------------------------------------------

# Setup caching
if not set -q __fish_cache_dir
    if set -q XDG_CACHE_HOME
        set -U __fish_cache_dir $XDG_CACHE_HOME/fish
    else
        set -U __fish_cache_dir $HOME/.cache/fish
    end
end

test -d $__fish_cache_dir; or mkdir -p $__fish_cache_dir

# Remove expired cache files
find $__fish_cache_dir -name '*.fish' -type f -mmin +1200 -delete

# -- OH MY POSH --------------------------------------------------------------------------------------------------------

set fish_greeting

if type -q oh-my-posh
    set -l omp_version (oh-my-posh --version)
    set -l omp_cache_file oh-my-posh_init_$omp_version.fish
    set -l omp_cache_path $__fish_cache_dir/$omp_cache_file

    # If cache for current version doesn't exist - clean up old ones
    if not test -f $omp_cache_path
        # Remove all old oh-my-posh fish caches
        set -l old_caches (path filter $__fish_cache_dir/oh-my-posh_init_*.fish)
        test -n "$old_caches"; and rm -f $old_caches
        # Clean up internal oh-my-posh cache (contains hardcoded path to binary)
        rm -rf $XDG_CACHE_HOME/oh-my-posh
        oh-my-posh cache build
    end

    cache_and_source $omp_cache_file "oh-my-posh init fish --config $XDG_CONFIG_HOME/oh-my-posh/config.json"
end

# -- BREW --------------------------------------------------------------------------------------------------------------

# Setup homebrew
if not set -q HOMEBREW_PREFIX
    cache_and_source brew_init.fish "/opt/homebrew/bin/brew shellenv"
end

# Add fish completions
if test -e "$HOMEBREW_PREFIX/share/fish/completions"
    set --append fish_complete_path "$HOMEBREW_PREFIX/share/fish/completions"
end

# Other homebrew vars
set -q HOMEBREW_NO_ANALYTICS || set -gx HOMEBREW_NO_ANALYTICS 1
set -q HOMEBREW_NO_AUTO_UPDATE || set -gx HOMEBREW_NO_AUTO_UPDATE 1

# -- ASDF --------------------------------------------------------------------------------------------------------------

# ASDF configuration code
if test -z $ASDF_DATA_DIR
    set _asdf_shims "$HOME/.asdf/shims"
else
    set _asdf_shims "$ASDF_DATA_DIR/shims"
end

# Do not use fish_add_path (added in Fish 3.2) because it
# potentially changes the order of items in PATH
if not contains $_asdf_shims $PATH
    set -gx --prepend PATH $_asdf_shims
end

set --erase _asdf_shims

set -q ASDF_CONFIG_FILE; or set -Ux ASDF_CONFIG_FILE $HOME/.config/asdf/asdfrc

add_completion asdf.fish "asdf completion fish"

## -- MISE -------------------------------------------------------------------------------------------------------------

if not set -q MISE_SHELL
    set -gx MISE_INSTALL_PATH (dirname (which brew))/mise
end

cache_and_source mise_init.fish "mise activate fish"
add_completion mise.fish "mise completion fish"

# -- CHEZMOI -----------------------------------------------------------------------------------------------------------

add_completion chezmoi.fish "chezmoi completion fish"

# -- FZF ---------------------------------------------------------------------------------------------------------------

if type -q fzf
    cache_and_source fzf_init.fish "fzf --fish"

    source $__fish_config_dir/conf.d/fzf/fzf.fish

    set -gx FZF_CUSTOM_OPTS "--reverse --height '100%' \
        --bind ctrl-b:preview-up,ctrl-f:preview-down \
        --bind ctrl-u:preview-half-page-up,ctrl-d:preview-half-page-down \
        --bind 'ctrl-/:change-preview-window(hidden|)' \
        --bind 'ctrl-e:execute(nvim {})' \
        --walker-skip .git,node_modules \
        --preview-window 'right:60%' \
        --multi"

    set -Ux FZF_DEFAULT_COMMAND "fd --type f --strip-cwd-prefix --hidden --follow"
    set -Ux FZF_CTRL_T_COMMAND "$FZF_CUSTOM_OPTS"
    set -Uxa FZF_CTRL_T_OPTS "--preview 'bat {}'"

    # https://github.com/PatrickF1/fzf.fish?tab=readme-ov-file#integrate-with-a-diff-highlighter
    set fzf_diff_highlighter diff-so-fancy
end

# -- ZOXIDE ------------------------------------------------------------------------------------------------------------

if type -q zoxide
    cache_and_source zoxide_init.fish "zoxide init fish"
end

# -- PAGER -------------------------------------------------------------------------------------------------------------

set -Ux MANPAGER nvim +Man!

# Ensure manpath is set to something so we can add to it.
set -q MANPATH || set -gx MANPATH ''

# Add more man page paths.
for manpath in (path filter $__fish_data_dir/man /usr/local/share/man /usr/share/man)
    set -a MANPATH $manpath
end

# -- VI MODE -----------------------------------------------------------------------------------------------------------

fish_vi_key_bindings

function rerender_on_bind_mode_change --on-variable fish_bind_mode
    if test "$fish_bind_mode" != "$FISH__BIND_MODE"
        set -gx FISH_BIND_MODE $fish_bind_mode
        omp_repaint_prompt
    end
end

function fish_default_mode_prompt --description "Display vi prompt mode"
    # This function is masked and does nothing
end

# -- BINDINGS ----------------------------------------------------------------------------------------------------------

function fish_user_key_bindings
    function bind_all_modes
        set -l key_en $argv[1]
        set -l key_ru $argv[2]
        set -l cmd $argv[3]

        for mode in (bind -L)
            bind -M $mode $key_en $cmd
            bind -M $mode $key_ru $cmd
        end
    end

    bind -M visual y fish_clipboard_copy
    bind -M normal yy fish_clipboard_copy
    bind p fish_clipboard_paste

    bind_all_modes ctrl-l ctrl-д clear_and_reinit
    bind_all_modes ctrl-e ctrl-у nvim
    bind_all_modes ctrl-y ctrl-н yazi

    functions -e bind_all_modes
end

# -- EDITOR ------------------------------------------------------------------------------------------------------------

# Set editor variables.
set -q EDITOR; or set -Ux EDITOR nvim
set -q VISUAL; or set -Ux VISUAL $EDITOR
set -q GIT_EDITOR; or set -Ux GIT_EDITOR $EDITOR

# -- SECRETS -----------------------------------------------------------------------------------------------------------

set -gx OPENAI_API_KEY "{{ (keepassxcAttribute "OPENAI" "API_KEY") }}"
set -gx GITLAB_TOKEN "{{ (keepassxcAttribute "GITLAB" "TOKEN") }}"
set -gx GITHUB_TOKEN "{{ (keepassxcAttribute "GITHUB" "TOKEN") }}"

# -- ABBRS -------------------------------------------------------------------------------------------------------------

if status is-interactive
    abbr -a -- e nvim
    abbr -a -- y yazi
    abbr -a -- ca chezmoi apply
    abbr -a -- ee nvim_config_pick
    abbr -a -- eu "nvim --headless '+Lazy! update' +qa && nvim"
    abbr -a -- f "for i in (seq 1 5); /usr/bin/time fish -i -c exit; end"
    abbr -a -- q exit

    abbr -a -- gb git branch -vv
    abbr -a -- gba git branch -vv -a
    abbr -a -- gbsup git branch --set-upstream-to=origin/\(git.branch_current\)
    abbr -a -- gbda git.branch_prune
    abbr -a -- gc! git commit -v --amend
    abbr -a -- gcn! git commit -v --no-edit --amend
    abbr -a -- gf git fetch
    abbr -a -- gfa git fetch --all --prune
    abbr -a -- gl git pull
    abbr -a -- gp git push
    abbr -a -- gpsup git push --set-upstream origin \(git.branch_current\)
    abbr -a -- gp! git push --force-with-lease
    abbr -a -- gco git checkout
    abbr -a -- gcod git checkout develop
    abbr -a -- gcom git checkout main
    abbr -a -- gfb git flow bugfix
    abbr -a -- gff git flow feature
    abbr -a -- gfr git flow release
    abbr -a -- gfh git flow hotfix
    abbr -a -- gfs git flow support
    abbr -a -- gfbs git flow bugfix start
    abbr -a -- gffs git flow feature start
    abbr -a -- gfrs git flow release start
    abbr -a -- gfhs git flow hotfix start
    abbr -a -- gfss git flow support start
    abbr -a -- gfbt git flow bugfix track
    abbr -a -- gfft git flow feature track
    abbr -a -- gfrt git flow release track
    abbr -a -- gfht git flow hotfix track
    abbr -a -- gfst git flow support track

    alias nr "npm run"

    alias ls "eza $eza_params"
    alias l "eza --git-ignore $eza_params"
    alias ll "eza --all --header --long $eza_params"
    alias llm "eza --all --header --long --sort=modified $eza_params"
    alias la "eza -lbhHigUmuSa"
    alias lt "eza --tree $eza_params"
end

# -- PATH --------------------------------------------------------------------------------------------------------------

set -gx CARGO_HOME $XDG_DATA_HOME/cargo
set -gx GNUPGHOME $XDG_DATA_HOME/gnupg
set -gx RUSTUP_HOME $XDG_DATA_HOME/rustup

set -gx GOPATH $HOME/Library/go

# https://webostv.developer.lge.com/develop/tools/cli-installation
set -gx LG_WEBOS_TV_SDK_HOME "$HOME/stv-tools/webos-sdk"
set -gx WEBOS_CLI_TV "$LG_WEBOS_TV_SDK_HOME/bin"

fish_add_path $GOPATH
fish_add_path "$HOMEBREW_PREFIX/opt/coreutils/libexec/gnubin" # https://github.com/git-quick-stats/git-quick-stats#macos-homebrew

# Add bin directories to path
fish_add_path --prepend (path filter \
	$HOME/bin \
	$HOME/.bin \
	$HOME/.local/bin \
	$HOME/.cargo/bin \
	$WEBOS_CLI_TV \
	$HOME/stv-tools/tizen-studio/tools/ide/bin \
	node_modules/.bin
)

# -- NODE --------------------------------------------------------------------------------------------------------------

set -gx NODE_REPL_HISTORY $XDG_STATE_HOME/node_repl_history

set -gx NPM_CONFIG_INIT_MODULE $XDG_CONFIG_HOME/npm/config/npm-init.js
set -gx NPM_CONFIG_CACHE $XDG_CACHE_HOME/npm
set -gx NPM_CONFIG_USERCONFIG $XDG_CONFIG_HOME/npm/npmrc

# -- OTHER -------------------------------------------------------------------------------------------------------------

set -gx SHELL /opt/homebrew/bin/fish
set -gx _GIT_PATHSPEC ":!package-lock.json" # https://git-quick-stats.sh/#docs

add_completion glab.fish "glab completion -s fish"

# ----------------------------------------------------------------------------------------------------------------------

functions -e cache_and_source
functions -e add_completion
